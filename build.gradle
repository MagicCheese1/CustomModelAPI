import org.gradle.api.JavaVersion

plugins {
    id 'java'
    id "com.github.johnrengelman.shadow" version "7.0.0"
    id "maven-publish"
}

group = 'io.github.magiccheese1'
version = '1.0-SNAPSHOT'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

dependencies {
    compileOnly 'com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT'
    implementation "co.aikar:acf-paper:0.5.0-SNAPSHOT"
}

shadowJar {
    minimize()
    relocate("co.aikar.commands", "io.github.magiccheese1.custommodelapi.acf")
    relocate("co.aikar.locales", "io.github.magiccheese1.custommodelapi.locales")
}

publishing {
    publications {
        maven(MavenPublication) {
            group = 'io.github.magiccheese1'
            artifactId = 'custommodelapi'
            version = '1.0'

            from components.java
        }
    }
}

tasks.build.dependsOn tasks.shadowJar


